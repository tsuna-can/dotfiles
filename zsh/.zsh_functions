# fzf history
function fzf-select-history() {
    local selected="$(history -nr 1 | awk '!a[$0]++' | fzf --exit-0 --query "$LBUFFER" --reverse )"
    if [ -n "$selected" ]; then
        BUFFER="$selected"
        CURSOR=$#BUFFER
    fi
    zle reset-prompt
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

# Seach and tansit to repo by ghq
function ghq-fzf() {
  local src=$(ghq list | fzf --preview "ls -laTp $(ghq root)/{} | tail -n+4 | awk '{print \$9\"/\"\$6\"/\"\$7 \" \" \$10}'")
  if [ -n "$src" ]; then
    BUFFER="cd $(ghq root)/$src"
    zle accept-line
  fi
  zle -R -c
}
zle -N ghq-fzf
bindkey '^]' ghq-fzf

# Copy kubernetes pod name
function fzf-copy-pod-name() {
  zle accept-line
  echo "Start pod selection..."
  local selected_line=$(kubectl get pods --no-headers | fzf --exit-0 --query "$LBUFFER" --prompt="Select a pod: " --height 40% --reverse)
  if [ -n "$selected_line" ]; then
    local selected_pod=$(echo "$selected_line" | awk '{print $1}')
    echo -n "$selected_pod" | pbcopy # only for macOS
    echo "Pod name '$selected_pod' copied to clipboard!"
  else
    echo "No pod selected."
  fi
  return 0
}
zle -N fzf-copy-pod-name
bindkey '^p' fzf-copy-pod-name

